asm SetteEMezzo

import StandardLibrary

signature: 

	enum domain Carte = {ASSO | DUE | TRE | QUATTRO | CINQUE | SEI | SETTE | REGINA | RE}
	enum domain Giocatori = {UTENTE | PC}
	enum domain Vincitori = {WIN_UTENTE | WIN_PC | NONDEFINITO}
	
	domain DomCarte subsetof Real
	
	dynamic monitored pescaUtente: Boolean
	dynamic controlled pescaPC: Boolean
	dynamic controlled mano: Giocatori -> DomCarte
	dynamic controlled vincitore: Vincitori
	dynamic controlled pescata: Giocatori -> Carte
	dynamic controlled soldi: Giocatori -> Integer
	
	static valoreCarte: Carte -> Real
	derived possibilePartita : Boolean
	
definitions:
	
	domain DomCarte = {0.0 : 20.0}
	
	function possibilePartita =
		if(soldi(UTENTE) = 0 or soldi(PC) = 0) then
			false
		else 
			true
		endif
	
	function valoreCarte($c in Carte) =
		switch($c)
			case ASSO : 1.0
			case DUE : 2.0
			case TRE : 3.0
			case QUATTRO : 4.0
			case CINQUE : 5.0
			case SEI : 6.0
			case SETTE : 7.0
			case REGINA : 0.5
			case RE : 0.5
		endswitch 
		
	rule r_azzeraPartita =	
		seq
			mano(UTENTE) := 0.0
			mano(PC) := 0.0
		endseq
		
	rule r_endOfGame =
			if(mano(UTENTE) >= 7.5 and mano(PC) >= 7.5) then
				r_azzeraPartita[]
			else if(mano(UTENTE) = 7.5 or mano(PC)>7.5) then
				par
					vincitore := WIN_UTENTE
					soldi(UTENTE) := soldi(UTENTE) + 1
					soldi(PC) := soldi(PC) - 1 
					r_azzeraPartita[]
				endpar
			else if(mano(PC) = 7.5 or mano(UTENTE)>7.5 or (pescaUtente=false and mano(PC)>mano(UTENTE))) then
				par
					vincitore := WIN_PC
					soldi(PC) := soldi(PC) + 1 
					soldi(UTENTE) := soldi(UTENTE) - 1
					r_azzeraPartita[]
				endpar
			endif
			endif
			endif
					
	
	rule r_pescata ($g in Giocatori, $c in Carte) =
		mano($g) := mano($g) + valoreCarte($c)
			
	
	rule r_partita =
		seq
			par
				vincitore := NONDEFINITO
				if(pescaUtente) then
					choose $c in Carte with true do
						seq
							pescata(UTENTE) := $c
							r_pescata[UTENTE,$c]
						endseq
				endif
				if(mano(PC)<=mano(UTENTE)) then
					choose $c1 in Carte with true do
						seq
							pescaPC := true
							pescata(PC) := $c1
							r_pescata[PC,$c1]
						endseq
				else
					pescaPC := false
				endif
			endpar
			r_endOfGame[]
		endseq
	
	
	main rule r_main = 
		if(possibilePartita) then
			r_partita[]
		endif

default init s0:
	function vincitore = NONDEFINITO
	function pescaPC = true
	function mano($g in Giocatori) = 0.0
	function soldi($g in Giocatori) = 5
